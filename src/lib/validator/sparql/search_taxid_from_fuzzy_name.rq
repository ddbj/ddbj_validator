DEFINE sql:select-option "order"
PREFIX id-tax: <http://identifiers.org/taxonomy/>
PREFIX tax: <http://ddbj.nig.ac.jp/ontologies/taxonomy/>

SELECT DISTINCT ?tax_no ?organism_name ?name_type ?scientific_name
FROM <<%= tax_graph_uri %>>
WHERE
{
  VALUES ?name_prop { tax:scientificName  tax:synonym tax:genbankSynonym tax:equivalentName
                      tax:authority tax:commonName tax:genbankCommonName tax:anamorph
                      tax:genbankAnamorph tax:teleomorph tax:unpublishedName}

  ?tax_id ?name_prop ?organism_name .
  ?organism_name bif:contains '"<%= organism_name_txt_search %>"' .
  ?name_prop rdfs:label ?name_type .
  FILTER (lcase(?organism_name) = lcase("<%= organism_name %>"))
  BIND (REPLACE(STR(?tax_id), "http://identifiers.org/taxonomy/", "") AS ?tax_no)
  ?tax_id a tax:Taxon ;
    tax:scientificName ?scientific_name .
  FILTER (!(?tax_id  = id-tax:32644 && ?name_prop !=  tax:scientificName))
}
