DEFINE sql:select-option "order"
PREFIX id-tax: <http://identifiers.org/taxonomy/>
PREFIX tax: <http://ddbj.nig.ac.jp/ontologies/taxonomy/>

SELECT DISTINCT ?tax_no ?organism_name ?name_type ?tax_type ?scientific_name
FROM <http://ddbj.nig.ac.jp/ontologies/taxonomy>
WHERE
{
  VALUES ?name_prop { tax:scientificName  tax:synonym tax:genbankSynonym tax:equivalentName
                      tax:authority tax:commonName tax:genbankCommonName tax:anamorph
                      tax:genbankAnamorph tax:teleomorph tax:unpublishedName}
  VALUES ?taxon_type { tax:Taxon tax:DummyTaxon }

  ?tax_id ?name_prop ?organism_name .
  ?organism_name bif:contains '"<%= organism_name_txt_search %>"' .
  ?name_prop rdfs:label ?name_type .
  FILTER (lcase(?organism_name) = lcase("<%= organism_name %>"))
  BIND (REPLACE(STR(?tax_id), "http://identifiers.org/taxonomy/", "") AS ?tax_no)
  ?tax_id a ?taxon_type ;
    tax:scientificName ?scientific_name .
  ?taxon_type rdfs:label ?tax_type .
}
